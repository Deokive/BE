plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.depth'
version = '0.0.1-SNAPSHOT'
description = 'deokive'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.11"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // DB
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // ORM
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Swagger
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13")

    // S3 Dependency
    // https://mvnrepository.com/artifact/io.awspring.cloud/spring-cloud-aws-starter-s3
    implementation("io.awspring.cloud:spring-cloud-aws-starter-s3:3.4.0")

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // Email Sender
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // Thymeleaf for Email Service
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

    // Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    //testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.security:spring-security-test'
    
    // Test Database (H2 for @DataJpaTest)
    testRuntimeOnly 'com.h2database:h2'

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.mockito:mockito-core'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
    
    // RestAssured for API Testing
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'io.rest-assured:json-path:5.4.0'
    testImplementation 'io.rest-assured:xml-path:5.4.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

// 리포트 생성 설정
tasks.named('jacocoTestReport') {
    dependsOn test // 리포트 생성 전 테스트 필수

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    // 제외할 클래스 패턴 정의
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    def myExcludes = [
            // 1. 애플리케이션 메인 클래스
            "**/*Application*",

            // 2. QueryDSL Q-Class 제외
            "**/Q*.*",

            // 3. 설정 파일, 예외, DTO, 모델 등 로직이 없는 클래스 제외
            "**/config/**",
            "**/dto/**",
            "**/exception/**",
            "**/model/**",

            // 4. 기타 인프라성 코드
            "**/common/**"
    ] + Qdomains

    classDirectories.setFrom(
            sourceSets.main.output.classesDirs.collect {
                fileTree(dir: it, exclude: myExcludes)
            }
    )
}

// 커버리지 검증 설정 (CI 빌드 실패 기준) -> 추후 다룰 예정
//tasks.named('jacocoTestCoverageVerification') {
//    // 리포트 생성 후 검증 실행
//    dependsOn 'jacocoTestReport'
//
//    violationRules {
//        rule {
//            // 룰을 체크할 단위 (CLASS, BUNDLE, PACKAGE 등)
//            element = 'CLASS'
//
//            // 제외할 클래스
//            excludes = [
//                    "**.*Application*",
//                    "**.Q*",
//                    "**.dto.**",
//                    "**.config.**",
//                    "**.exception.**",
//                    "**.common.**"
//            ]
//
//            // 조건 1: 브랜치 커버리지 (if/else 등 분기) 50% 이상
//            limit {
//                counter = 'BRANCH'
//                value = 'COVEREDRATIO'
//                minimum = 0.50
//            }
//
//            // 조건 2: 라인 커버리지 70% 이상
//            limit {
//                counter = 'LINE'
//                value = 'COVEREDRATIO'
//                minimum = 0.70
//            }
//        }
//    }
//}
